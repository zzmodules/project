using <stdio.h>::{ FILE, fopen, fread, fclose, printf }
using <stddef.h>::{ size_t }
using <sys/stat.h> as sysstat

using err::{ Err }
using file_stats
using file_stats::{ Stats }
using log
using slice::{ MutSlice }

export symbol FileNotOpenedError;

export struct File {
  FILE *file;
  char *pathname;
}

export fn make(File new mut *self, char *pathname)
  model safe(self->pathname)
  where safe(pathname)
{
  init(self, pathname);
  static_attest(safe(self->pathname));
}

export fn init(File mut *self, char *pathname)
  model safe(self->pathname)
  where safe(pathname)
{
  self->pathname = pathname;
  static_attest(safe(self->pathname));
}

export fn open(File mut *self, Err+ErrorTail mut *error) -> bool
  model safe(self->pathname)
{
  if 0 != self->pathname {
    static_attest(safe(self->pathname));
    self->file = unsafe<FILE *>(fopen(self->pathname, "r"));
  }

  if 0 == self->file {
    error->fail_with_errno("Failed to open file: %s", self->pathname);
    return false;
  }

  return true;
}

export fn close(File mut *self, Err+ErrorTail mut *error) -> bool {
  if 0 == self->file {
    error->fail(FileNotOpenedError, "File is not opened: %s", self->pathname);
    return false;
  }

  let status = unsafe<int>(fclose(as<FILE mut *>(self->file)));

  if 0 != status {
    error->fail_with_errno("Failed to close file: %s", self->pathname);
    return false;
  }

  self->file = 0;

  return true;
}

export fn read(File mut *self, MutSlice mut *mutslice, Err+ErrorTail mut *error) -> usize {
  if 0 == self->file {
    error->fail(FileNotOpenedError, "File is not opened: %s", self->pathname);
    return 0;
  }

  let mut stream = as<FILE mut *>(self->file);
  let mut out = mutslice->mem();
  let size = mutslice->slice.size;
  let status = unsafe<usize>(fread(out, sizeof(u8), size, stream));

  if size != status {
    error->fail_with_errno("Failed to read file: %s", self->pathname);
    return 0;
  }

  return size;
}

export fn stat(File *self, Err+ErrorTail mut *error) -> Stats
  model safe(self->pathname)
  where safe(self->pathname)
{
  return file_stats::stat_with_error_tail(self->pathname, error, ErrorTail);
}
